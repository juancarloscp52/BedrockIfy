plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/bedrockify.accesswidener")
	runs {
		// This adds a new gradle task that runs the datagen API: "gradlew runDatagenClient"
		datagenClient {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=bedrockify"

			runDir "build/datagen"
		}
	}

	createRemapConfigurations(sourceSets.test)
}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

repositories {
	mavenCentral()
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	}
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.ryanliptak.com/" } // AppleSkin
	maven { url "https://api.modrinth.com/maven" } // Iris, Panorama-Screens
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Dependancies
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	include(modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	})

	include(annotationProcessor(modApi("io.github.llamalad7:mixinextras-fabric:${project.mixin_extras_version}")))
	include(modImplementation("maven.modrinth:panorama-screens:${project.panoramascreens_version}"))

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	modCompileOnly("squeek.appleskin:appleskin-fabric:${project.apple_skin}:api") {
		transitive = false
	}
	modCompileOnly "maven.modrinth:fastload:${project.fastload_version}"

	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
	modTestImplementation "maven.modrinth:iris:${project.iris_version}"
}

test {
	useJUnitPlatform()
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
//	from(sourceSets.main.resources.srcDirs) {
//		include "fabric.mod.json"
//		expand "version": project.version
//	}
//
//	from(sourceSets.main.resources.srcDirs) {
//		exclude "fabric.mod.json"
//	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
tasks.register("sourcesJar", Jar) {
	dependsOn classes
	archiveClassifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
